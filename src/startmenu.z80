startmenu__:
;need to add in the options at the bottom
;add title at the top
;Need to set the filter flags
;Need to draw the menu box
;Need to show <status><name><type>
;Show 7 programs at a time


;need to set up the default screen
;need to clear it
  ei
  bcall(_DeleteTempPrograms)
  ld c,1
  call SetSpeed
  call sortVAT
  ld hl,saveSScreen
  ld (gbuf_temp),hl
  ld (BufPtr),hl
  ld (GrayBufPtr),hl
  call ZeroMem768
;gonna take advantage of A=0
  ld (iy+33),a    ;reset the text flags like a responsible adult, Zeda
  ld c,a
  call SetSpeed

;now we are going to draw the top and bottom fields
;Top line
  ;ld a,-1
  dec a   ;A is zero from ZeroMem768
  ld hl,saveSScreen+672
  ld b,12
  call SetSmallMem
;bottom line
  ld hl,saveSScreen+96
  ld b,12
  call SetSmallMem

;Header text (version number)
  ld hl,StrVersion
  ld bc,$0301
  call GPutSS
;Options
  ld hl,58
  ld (TextRow),hl
  call GPutSI
  .db "Gram AppV Asm  Exit Hook",0
;draw the bars between the options
  ld de,$0111
_:
  ld bc,0838h
  push de
  ld a,d
  call DrawRectToGraph
  pop de
  ld a,e
  add a,20
  ld e,a
  cp 96
  jr c,-_
;highlight the 'Grammer' filter option
  call highlight_Gram

;Need to set the filter flags.
  ld (iy+34),8

;highlight the hook option if the kook is active
  bit 0,(iy+35h)
  ld de,$124E
  call nz,highlight
;Draw the menu box
  call DrawRectToGraphI \ .db 16,10,44,64,1
  call DrawRectToGraphI \ .db 15,11,42,66,2
redraw_menu:
; Erase contents of the menu box
  call DrawRectToGraphI \ .db 16,11,42,64,0

;Reset the menu data
  ld hl,(progPtr)
  ld (VATptr),hl
;  ld (VATptr_next),hl
  call VATPtrToOP1
  ld hl,0
  ld (main_menu_cursor),hl   ;L=cursor, h=cursor_max
  ld (main_menu_index),hl

;Load the first seven entries (if possible)
_:
  call isStartMenu_valid
  jr nz,+_
  ld hl,(VATPtr)
  ld (VATPtr_next),hl   ;just a safe place to store it
  call NextVAT_valid_draw_advance_stepin
  jr startVATloop
_:
  call NextVAT_valid_draw
  jr c,main_menu_loop
  ld hl,(VATPtr)
  ld (VATPtr_next),hl   ;just a safe place to store it
startVATloop:
  call NextVAT_valid_draw
  jr c,+_
  ld a,(main_menu_cursor_max)
  cp 7
  jr nz,startVATloop
_:
  ld de,(VATPtr_next)
  ld hl,(VATPtr)
  ld (VATPtr_next),hl
  ld (VATPtr),de
main_menu_loop:
;here we need to draw the cursor highlight
  ld a,(main_menu_cursor)
  ld l,a
  add a,a
  add a,l
  add a,a
  add a,11    ;y coord
  ld b,6
  ld c,a
  ld de,$4010
  push bc
  ld a,2
  call DrawRectToGraph
  call GraphToLCD
  pop bc
  ld de,$4010
  ld a,2
  call DrawRectToGraph

main_menu_ui_loop:
  call GetKeyDebounce
  or a
  jr z,main_menu_ui_loop
  cp 15 \ ret z ;clear
  cp 50 \ ret z ;F4 (Exit)
  cp 53 \ jr nz,+_  ;F1 (Gram)
  ld a,(flags+34)
  xor 8
  ld (flags+34),a
  ld de,$1100
  jr highlight_option
_:
  cp 52 \ jr nz,+_  ;F2 (AppV)
  ld a,(flags+34)
  xor 64
  ld (flags+34),a
  ld de,1312h
highlight_option:
  call highlight
  jp redraw_menu
_:
  cp 51 \ jr nz,+_ ;F3
  ld a,(flags+34)
  xor 4
  ld (flags+34),a
  ld de,1326h
  jr highlight_option
_:
  cp 49 \ jr nz,+_ ;F5
  in a,(6)
  ld hl,9BCAh
  cp (hl)
  ld hl,TokenHook
;Manually setting hook
  ld (9BC8h),hl
  ld (9BCAh),a
  jr nz,SetTokenHook
  bit 0,(iy+35h)
  jr z,SetTokenHook
  res 0,(iy+35h)
  jr $+6
SetTokenHook:
  set 0,(iy+35h)
  ld de,$134E
  call highlight
  jp main_menu_loop
_:
  ld hl,main_menu_cursor
  dec a \ jr z,cur_dn   ;down
  sub 3 \ jr z,cur_up   ;up
  cp 5
  jp nz,main_menu_ui_loop
;here ENTER is pressed
;need to load the variable name into OP1
  ld a,(hl)
  or a
  jr z,selected_entry
_:
  push af
  call NextVAT_valid
  pop af
  dec a
  jr nz,-_
selected_entry:
  ld hl,(VATPtr)
  call VATPtrToOP1
  call isStartMenu_valid
  call z,SelectedProgOP1
  jp startmenu__
cur_dn:
  ld a,(hl)
  inc a
  inc hl
  cp (hl)
  jr nc,+_
  dec hl
  ld (hl),a
  jp main_menu_loop
_:
  cp 7
  jp nz,main_menu_ui_loop
;here we need to scroll down.
;This means we need to advance VATPtr and VATPtr_next
;draw VATPtr_next

;first, advance VATPtr_next
  ld hl,(VATPtr)
  push hl
  ld hl,(VATPtr_next)
  ld (VATPtr),hl
  call NextVAT_valid
  pop hl
  ld de,(VATPtr)
  ld (VATPtr),hl
  jp c,main_menu_ui_loop
_:
  ld (VATPtr_next),de
;now we know there are more entries
;shift the menu up, then draw the entry, then advance VATPtr
  ld de,86ECh+134
  ld hl,86ECh+206
  ld a,36
_:
  call mov8
  inc l \ inc hl
  inc l \ inc hl
  inc e \ inc de
  inc e \ inc de
  dec a
  jr nz,-_
;We need to clear the bottom 6 rows of pixels
  call DrawRectToGraphI \ .db 16,47,6,64,0
  ld a,6
  call draw_var_name
  call NextVAT_valid
  ld hl,(main_menu_index)
  inc hl
  ld (main_menu_index),hl
  jp main_menu_loop
cur_up:
  ld a,(hl)
  or a
  jr z,+_
  dec (hl)
  jp main_menu_loop
_:
;now we need to scroll up. Verify that main_menu_index!=0
  ld hl,(main_menu_index)
  ld a,h
  or l
  jp z,main_menu_ui_loop
  dec hl
  ld (main_menu_index),hl
  ld hl,(VATPtr)
  push hl
  ld de,(VATPtr_next)
  call PrevVAT_valid_with_start
;  call PrevVAT_with_start
  ld hl,(VATPtr)
  ld (VATPtr_next),hl
  pop hl
  ld (VATPtr),hl
  call PrevVAT_valid
;need to shift the screen up
  ld hl,86ECh+561
  ld de,86ECh+633
  ld a,36
_:
  ldd \ ldd \ ldd \ ldd
  ldd \ ldd \ ldd \ ldd
  dec l \ dec hl
  dec l \ dec hl
  dec e \ dec de
  dec e \ dec de
  dec a
  jr nz,-_
  call DrawRectToGraphI \ .db 16,11,6,64,0
  call draw_var_name
  jp main_menu_loop


highlight_Gram:
  ld de,$1100
highlight:
  ld bc,0739h
  ld a,2
  jp DrawRectToGraph
NextVAT_valid_draw:
  call NextVAT_valid
  ret c
NextVAT_valid_draw_advance_stepin:
  ld hl,main_menu_cursor_max
  ld a,(hl)
  inc (hl)
  call draw_var_name
  or a
  ret
NextVAT_valid:
  ld hl,(VATPtr)
  push hl
  call +_
  pop hl
  ret nc
  ld (VATPtr),hl
  ret
_:
  call NextVAT
  ret c
  call isStartMenu_valid
  jr nz,-_
  or a
  ret
draw_var_name:
  ld hl,(VATPtr)
  ld bc,-5
  add hl,bc
  ld c,a
  add a,a
  add a,c
  add a,a
  add a,11    ;y coord
  ld c,a
  ld b,4
  ld (textRow),bc
  xor a
  ld (OP1+9),a
  or (hl)
  ld a,' '
  jr z,+_
  ld a,'*'
_:
  call PutSC
  ld hl,OP1+1
  call GPutS
  ld a,16
  ld (textCol),a
  ld a,(OP1)
  cp 15h
  ld hl,s_appv
  jr z,+_
  ld hl,s_prog
_:
  jp GPutS
