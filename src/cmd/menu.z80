;Menu:
  ld hl,(BufPtr)
  ld (gbuf_temp),hl
  call ParseFullArg
  push bc
  call ParseNextFullArg
  pop de
  ld d,c
  push de
  call ParseNextFullArg
  pop hl
  ld (saveSScreen),hl
  ld b,9
  ld (saveSScreen+2),bc
  call ParseNextFullArg
  ld h,b
  ld l,c
  ld de,menuopts
  call GetGrammerText_DE; Title
  ex de,hl
  add hl,bc
  ld (hl),0
  inc hl
  push hl
  inc hl
  ex de,hl
  ld b,1

_: ;more Parameters
 push bc
 push de
 call ParseNextFullArg
 pop de
 ld h,b
 ld l,c
 cp $2B
 push af
 call GetGrammerText_DE
 ex de,hl
 add hl,bc
 ld (hl),0
 inc hl
 ex de,hl
 pop af
 pop bc

 jr nz,Menu_preselect1 ;kein Komma, kein preselect
 inc hl
 ld a,(hl) ;Komma vorhanden, ist dann ' vorhanden
 cp $AE ;'
 jr z,Menu_preselect2
 dec hl
 inc b
 jr -_

Menu_preselect1:
 xor a
 jr Menu_sele+4
Menu_preselect2:
 push bc
 call ParseNextFullArg_Inc
 ld a,c  ;c is wanted item = 1..7
 pop bc  ;b is max items
 ld d,b
 cp d
 jr c,Menu_sele
 ld a,d
Menu_sele:
 or a
 jr z,+_
 dec a
_:
 ld (TempWord3),a ;preselect
Exec_Menu:
 ld a,b
 pop hl
 ld (hl),a
 ld hl,saveSScreen
 call MenuSelect
 ld b,0
 inc l
 cp 15 ;test clear
 jr nz,+_
 ld l,b
_:

 ld c,l
 ret

L7c5d: ;Menu and jmp
 pop hl
 call MenuSelect
 ld de,(TempWord4)
 push de
 ret
_:
MenuSelect=-_-Menu+moduleExec
 ld c,(hl)
 inc hl
 ld e,(hl)
 inc hl
 ld d,(hl)
 inc hl
 ld b,(hl)
 ld hl,menuopts
 ld a,6
 push bc
 push de
 push hl
 call DrawRectToGraph
 pop hl
 pop de
 pop bc
 ld b,e
 inc b
 inc b
 inc c
 inc c
 push de
 push bc
 call vPutscr
 inc hl
 pop bc
 pop de
 ld a,c
 add a,8
 ld c,a
 push bc
 ld a,(hl)

 ld (TempWord3+1),a ;Anzahl items

 inc hl
 push af
 ld b,a
 add a,a
 add a,b
 add a,a
 ld b,a
 inc b
 inc b
 inc b
 dec c
 dec c
 ld a,6
 push hl
 push bc
 push de
 call DrawRectToGraph
 pop hl
 dec h
 dec h
 inc l
 ld (TempWord1),hl
 pop hl
 ld h,7
 inc l
 ld (TempWord2),hl
 pop hl
 pop af
 pop bc
 or a
 ret z
 ld de,(TempWord2)
 push de
 ld de,(TempWord1)
 push de

_: ;all rows
 push af
 push bc
 call vPutscr
 inc hl
 pop bc
 ld a,c
 add a,6
 ld c,a
 pop af
 dec a
 jr nz,-_ ;next row
 pop bc
 ld (TempWord1),bc
 pop bc
 ld (TempWord4),hl
 ld hl,(TempWord3)
;6*l+c->c
 ld a,l  ;0..7
 add a,a ;*2
 add a,l ;*3
 add a,a ;*6
 ld bc,(TempWord2)
 add a,c
 ld c,a
MenuDraw:
  ei
_:
  halt
  xor a
  out (1),a
  in a,(1)
  inc a
  jr nz,-_ ;port 1 ready loop
  di
  push hl
  ld de,(TempWord1) ;width, height
  push bc ;start_coord
  ld a,2  ;mode inv
  call DrawRectToGraph
  pop bc
  push bc
  call GraphToLCD
  pop bc
  ld de,(TempWord1)
  push bc
  ld a,2
  call DrawRectToGraph
_:  ;Test Enter, auf, ab
  call GetKey
  or a
  jr z,-_
  pop bc
  pop hl
  cp 9 ;Enter
  ret z
  cp 15 ;test clear
  ret z
  dec a
  jr nz,+_
  inc a
  jr L7d16 ;down = 1

_: ;test up
 sub 3
 jr nz,MenuDraw ;Menu draw
 dec a ;up=>-1

L7d16: ;down = 1
 ld d,a ;key
 add a,l
 cp h
 jr nc,MenuDraw ;Menu draw
 ld l,a  ;selected item
 ld a,d  ;+-1
 add a,a ;*2
 add a,d ;*3
 add a,a ;+-6
 add a,c
 ld c,a  ;new coord
 jr MenuDraw ;Menu draw
_:
vPutscr = -_-Menu+moduleExec
  ld (TextRow),bc
  ld bc,0
  ld d,b
_:
  ld a,(hl)
  cp d
  ret z
  push de
  inc bc
  push bc
  inc hl
  push hl
  ld b,a
  call VPutSC
  pop hl
  pop bc
  pop de
  jr -_
_:
menuopts = -_-Menu+moduleExec
